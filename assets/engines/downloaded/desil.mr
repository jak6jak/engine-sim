// Engine Sim V0.1.14A
// jamz_238
// ЯМЗ-238 240 @ 2100
// Created by oror 2023
// 1-5-4-2-6-3-7-8

import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label bore(130.0)
label stroke(169.8)
label compression_ratio(16.7)
label con_rod(222.0)
label compression_height(85.0 * units.mm) // or 100
label intake_valve_diameter(61.0)
label exhaust_valve_diameter(48.0)
label intake_valves(1.0)
label exhaust_valves(1.0)

label volume(14.866) // L
label redline(2150.0) // RPM
label intake_cfm((volume * redline) / 32) // CFM
label exhaust_cfm(intake_cfm * 1.75) // CFM

label cyl(8.0)
label row(2.0)
label cycle(720.0 * units.deg)
label rot(cycle / cyl)
label vee(60.0 * units.deg)
label rot90(90.0 * units.deg)
label rot180(180.0 * units.deg)
label rot360(360.0 * units.deg)

label intake_valve_lift(10.2) // ?
label exhaust_valve_lift(10.2) // ?
label IVO(20.0 * units.deg) //BTDC
label IVC(56.0 * units.deg) //ABDC
label EVO(56.0 * units.deg) //BBDC
label EVC(20.0 * units.deg) //ATDC

label intake_duration(IVO + IVC + rot180)
label exhaust_duration(EVO + EVC + rot180)

label ILC((-IVO + rot180 + IVC) / 2.0 + rot360)
label ELC((rot180 - EVO + EVC + rot360) / 2.0)

label con_rod_mass(6000.0) // g
label crank_mass(45.0) // ? kg
label flywheel_mass(40) // kg

label exhaust_delay_coeff(1.5)

public node eng_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 3000 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, rot * 0)
            .connect_wire(wires.wire5, rot * 1)
            .connect_wire(wires.wire4, rot * 2)
            .connect_wire(wires.wire2, rot * 3)
            .connect_wire(wires.wire6, rot * 4)
            .connect_wire(wires.wire3, rot * 5)
            .connect_wire(wires.wire7, rot * 6)
            .connect_wire(wires.wire8, rot * 7);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 256 * units.deg,
            gamma: 1.1,
            lift: intake_valve_lift * units.mm,
            steps: 100
        );
}

public node exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 256 * units.deg,
            gamma: 1.1,
            lift: exhaust_valve_lift * units.mm,
            steps: 100
        );
}

public node camshaft_builder {
    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: 0.0 * units.deg,
        base_radius: intake_valve_lift * 2.0 * units.mm
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile())
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile())
    
    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile())
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile())

    _intake_cam_0
        .add_lobe(ILC + 0 * rot)
        .add_lobe(ILC + 3 * rot)
        .add_lobe(ILC + 5 * rot)
        .add_lobe(ILC + 2 * rot)
    _exhaust_cam_0
        .add_lobe(ELC + 0 * rot)
        .add_lobe(ELC + 3 * rot)
        .add_lobe(ELC + 5 * rot)
        .add_lobe(ELC + 2 * rot)
        
    _intake_cam_1
        .add_lobe(ILC + 1 * rot)
        .add_lobe(ILC + 4 * rot)
        .add_lobe(ILC + 6 * rot)
        .add_lobe(ILC + 7 * rot)
    _exhaust_cam_1
        .add_lobe(ELC + 1 * rot)
        .add_lobe(ELC + 4 * rot)
        .add_lobe(ELC + 6 * rot)
        .add_lobe(ELC + 7 * rot)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node head {
    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    alias output __out: head;

    // port_flow.py v0.2
    // intake H: 61.0 L: 13.5 N: 1.0
    // exhaust H: 48.0 L: 13.5 N: 1.0
    // intake port area: 2484.097 mm2; saturated lift: 13.227 mm
    // exhaust port area: 1538.124 mm2; saturated lift: 10.408 mm
    // cylinder volume: 1858.252 cc; engine volume: 14866.016 cc 907.184 ci
    // 32 harmonic intake runner length: 22.523 cm; diameter: 4.521 cm
    // primary length: 331.572 cm, area: 10.593 cm2, diameter: 3.673 cm
    // collector diameter: 5.194 cm, area: 21.186 cm2
    // target power: 1935 RPM, torque: 1277 RPM
    function intake_flow(1.350 * units.mm)
    intake_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.350, 51.942)
        .add_flow_sample(2.700, 93.430)
        .add_flow_sample(4.050, 130.971)
        .add_flow_sample(5.400, 165.949)
        .add_flow_sample(6.750, 199.022)
        .add_flow_sample(8.100, 230.576)
        .add_flow_sample(9.450, 260.868)
        .add_flow_sample(10.800, 290.078)
        .add_flow_sample(12.150, 318.344)
        .add_flow_sample(13.500, 340.382)

    function exhaust_flow(1.350 * units.mm)
    exhaust_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.350, 39.450)
        .add_flow_sample(2.700, 70.674)
        .add_flow_sample(4.050, 98.792)
        .add_flow_sample(5.400, 124.894)
        .add_flow_sample(6.750, 149.496)
        .add_flow_sample(8.100, 172.903)
        .add_flow_sample(9.450, 195.317)
        .add_flow_sample(10.800, 210.788)
        .add_flow_sample(12.150, 211.130)
        .add_flow_sample(13.500, 211.547)

    cylinder_head head(
        chamber_volume: (circle_area(bore / 2.0) * stroke / compression_ratio) / 1000.0 * units.cc,
        intake_runner_volume: 361.501 * units.cc,
        intake_runner_cross_section_area: 16.050 * units.cm2,
        exhaust_runner_volume: 120.500 * units.cc,
        exhaust_runner_cross_section_area: 10.593 * units.cm2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node eng {
    alias output __out: engine;

    engine engine(
        name: "Belasco-Diesel 1100CI 32V",
        starter_torque: 6300 * units.Nm,
        starter_speed: 150 * units.rpm,
        redline: redline * units.rpm,
        fuel: fuel(
           name: "diesel",
            molecular_mass: 199.0 * units.g,
            energy_density: 45.5 * units.kJ / units.g,
            density: 0.850 * units.kg / units.L,
            molecular_afr: 10.25,
            max_turbulence_effect: 4.0,
            max_dilution_effect: 10.0,
            burning_efficiency_randomness: 0.12,
            max_burning_efficiency: 0.7
            ),
        throttle_gamma: 2.0,
        jitter: 1,
        noise: 1,
        hf_gain: 0.03,
        simulation_frequency: 4500,
        dyno_min_speed: 500 * units.rpm
    )

    wires wires()
    
    connecting_rod_parameters cr_params(
        mass: con_rod_mass * units.g,
        moment_of_inertia: rod_moment_of_inertia(
            mass: con_rod_mass * units.g,
            length: con_rod * units.mm
        ),
        center_of_mass: 0.0,
        length: con_rod * units.mm
    )

    label crank_moment(disk_moment_of_inertia(mass: crank_mass * units.kg, radius: (stroke / 2.0) * units.mm))
    label flywheel_moment(disk_moment_of_inertia(mass: flywheel_mass * units.kg, radius: (stroke * 3.5) * units.mm))
    label other_moment(disk_moment_of_inertia(mass: 1 * units.kg, radius: 1.0 * units.cm))

    crankshaft c0(
        throw: (stroke / 2.0) * units.mm,
        flywheel_mass: flywheel_mass * units.kg,
        mass: crank_mass * units.kg,
        friction_torque: 10.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0
    )

    rod_journal rj0(angle: rot * 0 + rot90 + vee / 2)
    rod_journal rj1(angle: rot * 3 + rot90 + vee / 2)
    rod_journal rj2(angle: rot * 5 + rot90 + vee / 2)
    rod_journal rj3(angle: rot * 2 + rot90 + vee / 2)
    
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: 9 * units.kg,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    cylinder_bank_parameters bank_params(
        bore: bore * units.mm,
        deck_height: (con_rod + stroke / 2.0) * units.mm + compression_height
    )

    label exhaust_crossection_area(21.186 * 2.0) // cm2

    label exhaust_pipe_length_0(400.0) // cm
    label exhaust_volume_0(exhaust_crossection_area * exhaust_pipe_length_0 / 100.0) // Litres
    
    label spacing_factor(1.25)
    label flange_density(1.0 * exhaust_delay_coeff)
    label primary_tube_length(100.0)

    intake intake(
        plenum_volume: 4.315 * units.L,
        plenum_cross_section_area: 64.201 * units.cm2,
        runner_length: 22.523 * units.cm,
        intake_flow_rate: k_carb(intake_cfm),
        idle_flow_rate: k_carb(0.03),
        idle_throttle_plate_position: 0.998,
        runner_flow_rate: k_carb(intake_cfm / (cyl / 2.0)),
        velocity_decay: 0.05
    )
    
    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(2000.0),
        primary_tube_length: 20.0 * units.inch,
        primary_flow_rate: k_carb(200.0),
        velocity_decay: 1.0, //0.5
        volume: 40.0 * units.L
    )

    exhaust_system exhaust0(es_params0, impulse_response: ir_lib.default_0, audio_volume: 5.0)
 
    cylinder_bank b0(bank_params, angle: vee / 2)
    cylinder_bank b1(bank_params, angle: -vee / 2)
    
    label sh((33.0 / exhaust_delay_coeff) * units.cm)
    label pl0 ((4.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl1 ((3.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl2 ((2.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl3 ((1.0 * bore * spacing_factor / flange_density) * units.mm)

    b0
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0 + sh,
            ignition_wire: wires.wire1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1 + sh,
            ignition_wire: wires.wire2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2 + sh,
            ignition_wire: wires.wire3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl3 + sh,
            ignition_wire: wires.wire4
        )
        
    b1
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0,
            ignition_wire: wires.wire5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1,
            ignition_wire: wires.wire6
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2,
            ignition_wire: wires.wire7
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl3,
            ignition_wire: wires.wire8
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    camshaft_builder camshaft()

    engine.add_ignition_module(
        eng_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )

    b0.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1
        )
    )

    // ignition_timing.py v0.2 a = 3.0, m = 1.16, r = 2150
    function timing_curve(215.0 * units.rpm)
    timing_curve
        .add_sample(0.0 * units.rpm, 0.0 * units.deg)
        .add_sample(215.0 * units.rpm, 3.0 * units.deg)
        .add_sample(430.0 * units.rpm, 3.5 * units.deg)
        .add_sample(645.0 * units.rpm, 4.0 * units.deg)
        .add_sample(860.0 * units.rpm, 4.7 * units.deg)
        .add_sample(1075.0 * units.rpm, 5.4 * units.deg)
        .add_sample(1290.0 * units.rpm, 6.3 * units.deg)
        .add_sample(1505.0 * units.rpm, 7.3 * units.deg)
        .add_sample(1720.0 * units.rpm, 8.5 * units.deg)
        .add_sample(1935.0 * units.rpm, 9.8 * units.deg)
        .add_sample(2150.0 * units.rpm, 11.4 * units.deg)
        .add_sample(2365.0 * units.rpm, -15.0 * units.deg)
        .add_sample(2580.0 * units.rpm, -60.0 * units.deg)
}

// МАЗ-504
public node veh {
   input mass: 7200 * units.kg;
   input diff_ratio: 4.98;
   input tire_radius: 507 * units.mm;

   alias output __out: vehicle;

   vehicle vehicle(
    mass: mass,
    diff_ratio: diff_ratio,
    tire_radius: tire_radius,
	max_brake_force: 30000 * units.N
   )
}

// МАЗ-504
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 3200 * units.Nm,
            max_clutch_flex: 6 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 70 * units.Nm / units.deg,
            clutch_damping: 1.0,
            simulate_flex: true
        )
			.add_gear(9.17)
            .add_gear(6.26)
            .add_gear(4.25)
            .add_gear(3.25)
            .add_gear(2.45)
            .add_gear(1.67)
            .add_gear(1.24)
            .add_gear(1.00);
}

public node main{
    run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)
}